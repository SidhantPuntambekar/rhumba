""" rules for basic QC checks """ 

raw = path.join("{data}", "fastq", "raw_data")
qc = path.join("{data}", "qc")

rule fastqc:
  input:
    path.join(raw, "{sample}.fastq.gz"),
  output:
    path.join(qc, "fastqc", "{sample}_fastqc/summary.txt"),
  params:
    out_dir = path.join(qc, "fastqc"),
    job_name = "fastqc.{sample}",
    memory = "select[mem>16] rusage[mem=16]",
  log:
    path.join("logs", "fastqc", "{sample}.txt") 
  message:
    "fastqc analysis of {wildcards.sample} "
  threads:
    6 
  resources: all_threads=6
  shell:
    """
    mkdir -p {params.out_dir}
    fastqc \
      -t {threads} \
      -f fastq \
      -o {params.out_dir} \
      {input} 
    """

def _group_samples(wildcards):
    input_files = expand("{data}/star/{sample}_Aligned.sortedByCoord.out.bam", 
      data = DATA, sample=SAMPLES)
    input_str = ",".join(input_files)
    return input_str

rule gene_coverage:
  input:
    expand("{data}/star/{sample}_Aligned.sortedByCoord.out.bam",
      data = DATA,
      sample = SAMPLES) 
  output:
    path.join(qc, "rseqc", "all_samples.geneBodyCoverage.txt")
  params:
    bams = _group_samples,
    outname = path.join(qc, "rseqc", "all_samples"),
    job_name = 'bam.libstats',
    memory =  "select[mem>16] rusage[mem=16] "
  message:
    "getting alignment stats "
  log:
    path.join("logs", "rseqc", "coveage.txt") 
  resources: all_threads=1
  shell:
    """
    geneBody_coverage.py \
      -i {params.bams} \
      -r {TRANSCRIPTS_BED} \
      -l 100 \
      -f "pdf" \
      -o {params.outname}

    """


rule single_lib_stats:
  input:
    bam = "{data}/star/{sample}_Aligned.sortedByCoord.out.bam"
  output:
    bam_stat = path.join(qc, "rseqc", "{sample}_bam_stat.txt"),
    expt = path.join(qc, "rseqc", "{sample}_lib_type.txt"),
    dist = path.join(qc, "rseqc", "{sample}_read_dist.txt"),
  params:
    job_name = '{sample}.libstats',
    memory =  "select[mem>16] rusage[mem=16] "
  message:
    "getting alignment stats "
  log:
    path.join("logs", "rseqc", "{sample}.txt") 
  resources: all_threads=1
  shell:
    """
    bam_stat.py -i {input.bam} -q 10 > {output.bam_stat}
    infer_experiment.py -q 10 -i {input.bam} -r {TRANSCRIPTS_BED} -q 10 > {output.expt}
    read_distribution.py -i {input.bam} -r {TRANSCRIPTS_BED} > {output.dist}
    """
